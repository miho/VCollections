// enable intellij integration & load vmf version to be used
buildscript {

    ext.commonProps = new Properties()
    file(new File(rootProject.getRootDir(),"config/common.properties")).
            withInputStream { {commonProps.load(it) }}

    ext.vmfPluginIntelliJIntegration = true

    repositories {
        mavenCentral()
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.16.1"
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '3.1.0'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

apply plugin: 'java'
apply from: 'gradle/publishing.gradle'

wrapper {
    gradleVersion = '8.14.2'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

// JDK 8 is way too strict about Javadoc style
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        java   = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
        fxml   = 'XML_STYLE'
    }
    ext.yearFirst   = '2017'
    ext.yearNow     = new java.text.SimpleDateFormat("yyyy").format(new Date())
    ext.author = 'Michael Hoffer <info@michaelhoffer.de>'
    exclude '**/*.svg'
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'eu.mihosoft.vcollections')
    }
}

test {
    //we want display the following test events
    testLogging {
        //        events "PASSED", "STARTED", "FAILED", "SKIPPED"
        showStandardStreams = true
    }
}

